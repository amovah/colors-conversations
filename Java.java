/**
 * Converts an HSL color value to RGB. Conversion formula
 * adapted from http://en.wikipedia.org/wiki/HSL_color_space.
 * Assumes h, s, and l are contained in the set [0, 1] and
 * returns r, g, and b in the set [0, 255].
 *
 * @param h       The hue
 * @param s       The saturation
 * @param l       The lightness
 * @return int array, the RGB representation
 */

public static int[] hslToRgb(float h, float s, float l){
  float r, g, b;

  if (s == 0f) {
    r = g = b = l; // achromatic
  } else {
    float q = l < 0.5 ? l * (1 + s) : l + s - l * s;
    float p = 2 * l - q;
    r = hueToRgb(p, q, h + 1f/3f);
    g = hueToRgb(p, q, h);
    b = hueToRgb(p, q, h - 1f/3f);
  }
  int[] rgb = {(int) (r * 255), (int) (g * 255), (int) (b * 255)};
  return rgb;
}

/** Helper method that converts hue to rgb */
public static float hueToRgb(float p, float q, float t) {
  if (t < 0f)
  t += 1f;
  if (t > 1f)
  t -= 1f;
  if (t < 1f/6f)
  return p + (q - p) * 6f * t;
  if (t < 1f/2f)
  return q;
  if (t < 2f/3f)
  return p + (q - p) * (2f/3f - t) * 6f;
  return p;
}

/**
 * Converts an RGB color value to HSL. Conversion formula
 * adapted from http://en.wikipedia.org/wiki/HSL_color_space.
 * Assumes pR, pG, and bpBare contained in the set [0, 255] and
 * returns h, s, and l in the set [0, 1].
 *
 * @param pR       The red color value
 * @param pG       The green color value
 * @param pB       The blue color value
 * @return float array, the HSL representation
 */

public static float[] rgbToHsl(int pR, int pG, int pB) {
  float r = pR / 255f;
  float g = pG / 255f;
  float b = pB / 255f;

  float max = (r > g && r > b) ? r : (g > b) ? g : b;
  float min = (r < g && r < b) ? r : (g < b) ? g : b;

  float h, s, l;
  l = (max + min) / 2.0f;

  if (max == min) {
    h = s = 0.0f;
  } else {
    float d = max - min;
    s = (l > 0.5f) ? d / (2.0f - max - min) : d / (max + min);

    if (r > g && r > b)
    h = (g - b) / d + (g < b ? 6.0f : 0.0f);

    else if (g > b)
    h = (b - r) / d + 2.0f;

    else
    h = (r - g) / d + 4.0f;

    h /= 6.0f;
  }

  float[] hsl = {h, s, l};
  return hsl;
}
